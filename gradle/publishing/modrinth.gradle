apply plugin: 'com.modrinth.minotaur'

import java.nio.file.Files

modrinth {
    token = System.getenv('MODRINTH_TOKEN')
    projectId = mod_modrinth_id
    versionNumber = mod_version
    versionName = "${mod_name} ${mod_version}"
    versionType = mod_version.contains('-alpha') ? 'alpha' : mod_version.contains('-beta') ? 'beta' : 'release'
    uploadFile = file("build/libs/${mod_id}-${loader_name}-${minecraft_version}-${mod_version}.jar")
    changelog = extractChangelog(rootProject.file('CHANGELOG.md'), mod_version)
    gameVersions = "${minecraft_version_list}".split(",").toList()
    def targets = []
    if (project.ext.find('fuse_forge') == true) {
        targets.add('forge')
    }
    if (project.ext.find('fuse_fabric') == true) {
        targets.add('fabric')
    }
    if (project.ext.find('fuse_quilt') == true) {
        targets.add('quilt')
    }
    if (project.ext.find('fuse_neoforge') == true) {
        targets.add('neoforge')
    }
    loaders = targets
    println("${mod_name} ${mod_version} ${minecraft_version} \t${loaders}")
}

static def extractChangelog(File changelogFile, String modVersion) {
    def text = Files.readString(changelogFile.toPath())
    def split = text.split('----------\n\n')
    for (final def log in split) {
        if (log.startsWith("### $modVersion")) {
            return log.replaceFirst("### $modVersion", '').trim()
        }
    }
    throw new IllegalStateException("$modVersion not found in changelog")
}
